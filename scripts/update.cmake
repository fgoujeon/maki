#Copyright Florian Goujeon 2021 - 2025.
#Distributed under the Boost Software License, Version 1.0.
#(See accompanying file LICENSE or copy at
#https://www.boost.org/LICENSE_1_0.txt)
#Official repository: https://github.com/fgoujeon/maki

cmake_minimum_required(VERSION 3.23)

set(CURRENT_DIR "${CMAKE_CURRENT_LIST_DIR}")
set(TEMPLATES_DIR "${CURRENT_DIR}/update/templates")
cmake_path(GET CURRENT_DIR PARENT_PATH PROJECT_DIR)
set(PROJECT_INCLUDE_DIR "${PROJECT_DIR}/include")

# Read info file
file(READ "${CURRENT_DIR}/update/info.json" INFO_JSON)
string(JSON VERSION_MAJOR GET "${INFO_JSON}" version major)
string(JSON VERSION_MINOR GET "${INFO_JSON}" version minor)
string(JSON VERSION_PATCH GET "${INFO_JSON}" version patch)

# List include files
file(
    GLOB_RECURSE
    INCLUDE_FILES
    RELATIVE "${PROJECT_DIR}"
    "${PROJECT_INCLUDE_DIR}/*.*")
list(LENGTH INCLUDE_FILES INCLUDE_FILE_COUNT)

# Make string containing indented list of include files
unset(INDENTED_INCLUDE_FILES)
foreach(INCLUDE_FILE IN LISTS INCLUDE_FILES)
    set(INDENTED_INCLUDE_FILES "${INDENTED_INCLUDE_FILES}\n        ${INCLUDE_FILE}")
endforeach()

# Print info
message("Version ${VERSION_MAJOR}.${VERSION_MINOR}.${VERSION_PATCH}")
message("${INCLUDE_FILE_COUNT} include files")

# List template files
file(
    GLOB_RECURSE
    TEMPLATE_FILES
    RELATIVE "${TEMPLATES_DIR}"
    "${TEMPLATES_DIR}/*.*")

# Generate files from template files and info
foreach(TEMPLATE_FILE IN LISTS TEMPLATE_FILES)
    set(SRC_FILE "${TEMPLATES_DIR}/${TEMPLATE_FILE}")
    set(DST_FILE "${PROJECT_DIR}/${TEMPLATE_FILE}")
    set(GENERATOR_COMMENT "Generated by \"scripts/update.cmake\" from \"scripts/update/templates/${TEMPLATE_FILE}\"")

    message("Updating ${DST_FILE}...")

    configure_file(
        "${SRC_FILE}"
        "${DST_FILE}"
        @ONLY)
endforeach()
